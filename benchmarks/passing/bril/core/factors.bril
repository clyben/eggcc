# Prints out the integer factors of the input
# Trial division algorithm: https://en.wikipedia.org/wiki/Trial_division

# input: a positive integer
# output: the integer's factors

@main() {
  loop_incr: int = const 1;
  loop_counter: int = const 10;
  loop_bound: int = const 100000;
.loop_cond:
  loop_cond: bool = lt loop_counter loop_bound;
  br loop_cond .loop_body .loop_done;
.loop_body:
  call @orig_main loop_counter;
  loop_counter: int = add loop_counter loop_incr;
  jmp .loop_cond;
.loop_done:
}

@orig_main(num: int) {
  zer: int = const 0;
  one: int = const 1;
  fac: int = const 2;

.loopcheck:
  ispos: bool = lt one num;
  br ispos .loopbody .loopend;
  
.loopbody:
  quo: int = div num fac;
  tmp: int = mul quo fac;
  mod: int = sub num tmp;     # num % fac
  iszero: bool = eq mod zer;
  br iszero .ifyes .ifno;

.ifyes:
  print fac;
  num: int = div num fac;
  jmp .loopcheck;

.ifno:
  fac: int = add fac one; 
  jmp .loopcheck;

.loopend:
  # nothing, just drop out to EoF
}