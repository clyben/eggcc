# ARGS: 14500
# compute the sum of [1, n] by both loop and formula
# and compare them to see if the result is the same

@main(loop_bound: int) {
  loop_incr: int = const 1;
  loop_counter: int = const 10;
  final_output: int = const 0;
.loop_cond:
  loop_cond: bool = lt loop_counter loop_bound;
  br loop_cond .loop_body .loop_done;
.loop_body:
  output: int = call @orig_main loop_counter;
  final_output: int = add final_output output;
  loop_counter: int = add loop_counter loop_incr;
  jmp .loop_cond;
.loop_done:
  print final_output;
}

@orig_main(n: int): int {
    sum: int = const 0;
    first: int = call @sum_by_loop n;
    second: int = call @sum_by_formula n;
    isSame: bool = eq first second;
    sum: int = add sum first;
    sum: int = add sum second;
    br isSame .addone .done;
.addone:
    one: int = const 1;
    sum: int = add sum one;
.done:
    ret sum;
}

@sum_by_loop(n: int): int {
    one: int = const 1;
    sum: int = const 0;
    i: int = const 1;
    
.for_start:
    con: bool = le i n;
    br con .for .end;
.for:
    sum: int = add sum i;
    i: int = add i one;
    jmp .for_start;
.end:
    ret sum;
}

# sum = (1 + n) * n / 2
@sum_by_formula(n: int): int {
    one: int = const 1;
    two: int = const 2;
    n_1: int = add one n;
    multi: int = mul n_1 n;
    sum: int = div multi two;
    ret sum;
}