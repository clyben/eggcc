## syr2k computes a symmetric rank k update

@main {
    # constants
    # dimensions correspond to Polybench MEDIUM_DATASET

    M: int = const 200;
    fM: int = const 200;
    N: int = const 240;
    fN: int = const 240;

    one: int = const 1;
    fzero: int = const 0;

    # initialize arrays
    C: ptr<int> = call @matrix_new N N;
    A: ptr<int> = call @matrix_new N M;
    alpha: int = const 3;
    beta: int = const 2;
    call @init C A M fM N fN;

    # main computation

    i: int = const 0;
.main_i:
    cond: bool = lt i N;
    br cond .main_i_body .main_i_done;
.main_i_body:
    j: int = const 0;
.inner_j:
    cond: bool = le j i;
    br cond .inner_j_body .inner_j_done;
.inner_j_body:
    call @matrix_scale C i j N beta;
    j: int = add j one;
    jmp .inner_j;
.inner_j_done:
    k: int = const 0;
.main_k:
    cond: bool = lt k M;
    br cond .main_k_body .main_k_done;
.main_k_body:
    j: int = const 0;
.main_j:
    cond: bool = le j i;
    br cond .main_j_body .main_j_done;
.main_j_body:
    Ajk: int = call @matrix_get A j k M;
    Aik: int = call @matrix_get A i k M;
    incr: int = mul alpha Aik;
    incr: int = mul incr Ajk;
    call @matrix_incr C i j N incr;
    j: int = add j one;
    jmp .main_j;
.main_j_done:
    k: int = add k one;
    jmp .main_k;
.main_k_done:
    i: int = add i one;
    jmp .main_i;
.main_i_done:

    call @matrix_print C N N;

    free C;
    free A;
}

@init(C: ptr<int>, A: ptr<int>,
      M: int, fM: int, N: int, fN: int) {
    one: int = const 1;
    fone: int = const 1;
    ftwo: int = const 2;

    i: int = const 0;
    fi: int = const 0;
.init_A_i:
    cond: bool = lt i N;
    br cond .init_A_i_body .init_A_i_done;
.init_A_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_A_j:
    cond: bool = lt j M;
    br cond .init_A_j_body .init_A_j_done;
.init_A_j_body:
    val: int = mul fi fj;
    val: int = add val fone;
    val: int = call @fmod val fN;
    val: int = div val fN;
    call @matrix_set A i j M val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_A_j;
.init_A_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_A_i;
.init_A_i_done:

    i: int = const 0;
    fi: int = const 0;
.init_C_i:
    cond: bool = lt i N;
    br cond .init_C_i_body .init_C_i_done;
.init_C_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_C_j:
    cond: bool = lt j N;
    br cond .init_C_j_body .init_C_j_done;
.init_C_j_body:
    val: int = mul fi fj;
    val: int = add val ftwo;
    val: int = call @fmod val fM;
    val: int = div val fM;
    call @matrix_set C i j N val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_C_j;
.init_C_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_C_i;
.init_C_i_done:
}

@matrix_new(Nrow: int, Ncol: int): ptr<int> {
    total: int = mul Nrow Ncol;
    ptr: ptr<int> = alloc total;
    ret ptr;
}

@matrix_loc(mtx: ptr<int>, row: int, col: int, Ncol: int): ptr<int> {
    row_offset: int = mul row Ncol;
    offset: int = add row_offset col;
    new_ptr: ptr<int> = ptradd mtx offset;
    ret new_ptr;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_get(mtx: ptr<int>, row: int, col: int, Ncol: int): int {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    val: int = load ptr;
    ret val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_set(mtx: ptr<int>, row: int, col: int, Ncol: int, val: int) {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    store ptr val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_incr(mtx: ptr<int>, row: int, col: int, Ncol: int, incr: int) {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    val: int = load ptr;
    new_val: int = add val incr;
    store ptr new_val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_scale(mtx: ptr<int>, row: int, col: int, Ncol: int, scale: int) {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    val: int = load ptr;
    new_val: int = mul val scale;
    store ptr new_val;
}

@matrix_print(mtx: ptr<int>, Nrow: int, Ncol: int) {
    i: int = const 0;
    one: int = const 1;
    total: int = mul Nrow Ncol;
.while:
    cond: bool = lt i total;
    br cond .body .done;
.body:
    mtx_loc: ptr<int> = ptradd mtx i;
    val: int = load mtx_loc;
    print val;
    i: int = add i one;
    jmp .while;
.done:
}

# Search for n % m where n and m are ints by
# iteratively subtracting the largest m*2^k that
# fits inside n. 
# Takes O((log n/m)^2) time.
# NOTE: In C, this can be done with a built in 
# function, but this is the best we can do.
@fmod(n: int, m: int): int {
    zero: int = const 0;
    two: int = const 2;
    rem: int = id n;
.while:
    cond: bool = ge rem m;
    br cond .body .done;
.body:
    decr: int = id m;
.while_inner:
    diff: int = sub rem decr;
    cond: bool = ge diff zero;
    br cond .body_inner .done_inner;
.body_inner:
    decr: int = mul decr two;
    jmp .while_inner;
.done_inner:
    decr: int = div decr two;
    rem: int = sub rem decr;
    jmp .while;
.done:
    ret rem;
}