## 3mm computes G := (A * B) * (C * D)
## for some procedurally generated matrices A, B, C, D

@main {
    # constants
    # dimensions correspond to Polybench MEDIUM_DATASET

    NI: int = const 180;
    fNI: int = const 180;
    NJ: int = const 190;
    fNJ: int = const 190;
    NK: int = const 200;
    fNK: int = const 200;
    NL: int = const 210;
    fNL: int = const 210;
    NM: int = const 220;
    fNM: int = const 220;

    one: int = const 1;
    fzero: int = const 0;

    # initialize arrays
    A: ptr<int> = call @matrix_new NI NK;
    B: ptr<int> = call @matrix_new NK NJ;
    C: ptr<int> = call @matrix_new NJ NM;
    D: ptr<int> = call @matrix_new NM NL;
    call @init A B C D NI fNI NJ fNJ NK fNK NL fNL NM fNM;
    call @matrix_print A NI NK;
    call @matrix_print B NK NJ;
    call @matrix_print C NJ NM;
    call @matrix_print D NM NL;
    # main computation
    # computes G := (A * B) * (C * D)

    # first compute E := A * B 
    E: ptr<int> = call @matrix_new NI NJ;

    i: int = const 0;
.part1_i:
    cond: bool = lt i NI;
    br cond .part1_i_body .part1_i_done;
.part1_i_body:
    j: int = const 0;
.part1_j:
    cond: bool = lt j NJ;
    br cond .part1_j_body .part1_j_done;
.part1_j_body:
    call @matrix_set E i j NJ fzero;
    k: int = const 0;
.part1_k:
    cond: bool = lt k NK;
    br cond .part1_k_body .part1_k_done;
.part1_k_body:
    Aik: int = call @matrix_get A i k NK;
    Bkj: int = call @matrix_get B k j NJ;
    incr: int = mul Aik Bkj;
    call @matrix_incr E i j NJ incr;
    k: int = add k one;
    jmp .part1_k;
.part1_k_done:
    j: int = add j one;
    jmp .part1_j;
.part1_j_done:
    i: int = add i one;
    jmp .part1_i;
.part1_i_done:
    
    # then compute F := C * D
    F: ptr<int> = call @matrix_new NJ NL;

    i: int = const 0;
.part2_i:
    cond: bool = lt i NJ;
    br cond .part2_i_body .part2_i_done;
.part2_i_body:
    j: int = const 0;
.part2_j:
    cond: bool = lt j NL;
    br cond .part2_j_body .part2_j_done;
.part2_j_body:
    call @matrix_set F i j NL fzero;
    k: int = const 0;
.part2_k:
    cond: bool = lt k NM;
    br cond .part2_k_body .part2_k_done;
.part2_k_body:
    Cik: int = call @matrix_get C i k NM;
    Dkj: int = call @matrix_get D k j NL;
    incr: int = mul Cik Dkj;
    call @matrix_incr F i j NL incr;
    k: int = add k one;
    jmp .part2_k;
.part2_k_done:
    j: int = add j one;
    jmp .part2_j;
.part2_j_done:
    i: int = add i one;
    jmp .part2_i;
.part2_i_done:

    # finally compute G := E * F 
    G: ptr<int> = call @matrix_new NI NL;

    i: int = const 0;
.part3_i:
    cond: bool = lt i NI;
    br cond .part3_i_body .part3_i_done;
.part3_i_body:
    j: int = const 0;
.part3_j:
    cond: bool = lt j NL;
    br cond .part3_j_body .part3_j_done;
.part3_j_body:
    call @matrix_set G i j NL fzero;
    k: int = const 0;
.part3_k:
    cond: bool = lt k NJ;
    br cond .part3_k_body .part3_k_done;
.part3_k_body:
    Eik: int = call @matrix_get E i k NJ;
    Fkj: int = call @matrix_get F k j NL;
    incr: int = mul Eik Fkj;
    call @matrix_incr G i j NL incr;
    k: int = add k one;
    jmp .part3_k;
.part3_k_done:
    j: int = add j one;
    jmp .part3_j;
.part3_j_done:
    i: int = add i one;
    jmp .part3_i;
.part3_i_done:

    call @matrix_print G NI NL;

    free A;
    free B;
    free C;
    free D;
    free E;
    free F;
    free G;
}

@init(A: ptr<int>, B: ptr<int>, C: ptr<int>, D: ptr<int>,
      NI: int, fNI: int, NJ: int, fNJ: int, NK: int, fNK: int, NL: int, fNL: int, NM: int, fNM: int) {
    one: int = const 1;
    fzero: int = const 0;
    fone: int = const 1;
    ftwo: int = const 2;
    fthree: int = const 3;
    ffive: int = const 5;

    i: int = const 0;
    fi: int = const 0;
.init_A_i:
    cond: bool = lt i NI;
    br cond .init_A_i_body .init_A_i_done;
.init_A_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_A_j:
    cond: bool = lt j NK;
    br cond .init_A_j_body .init_A_j_done;
.init_A_j_body:
    val: int = mul fi fj;
    val: int = add val fone;
    val: int = call @fmod val fNI;
    denom: int = mul ffive fNI;
    val: int = div val denom;
    call @matrix_set A i j NK val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_A_j;
.init_A_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_A_i;
.init_A_i_done:

    i: int = const 0;
    fi: int = const 0;
.init_B_i:
    cond: bool = lt i NK;
    br cond .init_B_i_body .init_B_i_done;
.init_B_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_B_j:
    cond: bool = lt j NJ;
    br cond .init_B_j_body .init_B_j_done;
.init_B_j_body:
    val: int = add fj fone;
    val: int = mul fi val;
    val: int = add val ftwo;
    val: int = call @fmod val fNJ;
    denom: int = mul ffive fNJ;
    val: int = div val denom;
    call @matrix_set B i j NJ val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_B_j;
.init_B_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_B_i;
.init_B_i_done:
    
    i: int = const 0;
    fi: int = const 0;
.init_C_i:
    cond: bool = lt i NJ;
    br cond .init_C_i_body .init_C_i_done;
.init_C_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_C_j:
    cond: bool = lt j NM;
    br cond .init_C_j_body .init_C_j_done;
.init_C_j_body:
    val: int = add fj fthree;
    val: int = mul fi val;
    val: int = call @fmod val fNL;
    denom: int = mul ffive fNL;
    val: int = div val denom;
    call @matrix_set C i j NM val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_C_j;
.init_C_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_C_i;
.init_C_i_done:

    i: int = const 0;
    fi: int = const 0;
.init_D_i:
    cond: bool = lt i NM;
    br cond .init_D_i_body .init_D_i_done;
.init_D_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_D_j:
    cond: bool = lt j NL;
    br cond .init_D_j_body .init_D_j_done;
.init_D_j_body:
    val: int = add fj ftwo;
    val: int = mul fi val;
    val: int = add val ftwo;
    val: int = call @fmod val fNK;
    denom: int = mul ffive fNK;
    val: int = div val denom;
    call @matrix_set D i j NL val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_D_j;
.init_D_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_D_i;
.init_D_i_done:
}

@matrix_new(Nrow: int, Ncol: int): ptr<int> {
    total: int = mul Nrow Ncol;
    ptr: ptr<int> = alloc total;
    ret ptr;
}

@matrix_loc(mtx: ptr<int>, row: int, col: int, Ncol: int): ptr<int> {
    row_offset: int = mul row Ncol;
    offset: int = add row_offset col;
    new_ptr: ptr<int> = ptradd mtx offset;
    ret new_ptr;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_get(mtx: ptr<int>, row: int, col: int, Ncol: int): int {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    val: int = load ptr;
    ret val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_set(mtx: ptr<int>, row: int, col: int, Ncol: int, val: int) {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    store ptr val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_incr(mtx: ptr<int>, row: int, col: int, Ncol: int, incr: int) {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    val: int = load ptr;
    new_val: int = add val incr;
    store ptr new_val;
}

@matrix_print(mtx: ptr<int>, Nrow: int, Ncol: int) {
    i: int = const 0;
    one: int = const 1;
    total: int = mul Nrow Ncol;
.while:
    cond: bool = lt i total;
    br cond .body .done;
.body:
    mtx_loc: ptr<int> = ptradd mtx i;
    val: int = load mtx_loc;
    print val;
    i: int = add i one;
    jmp .while;
.done:
}

# Search for n % m where n and m are ints by
# iteratively subtracting the largest m*2^k that
# fits inside n. 
# Takes O((log n/m)^2) time.
# NOTE: In C, this can be done with a built in 
# function, but this is the best we can do.
@fmod(n: int, m: int): int {
    zero: int = const 0;
    two: int = const 2;
    rem: int = id n;
.while:
    cond: bool = ge rem m;
    br cond .body .done;
.body:
    decr: int = id m;
.while_inner:
    diff: int = sub rem decr;
    cond: bool = ge diff zero;
    br cond .body_inner .done_inner;
.body_inner:
    decr: int = mul decr two;
    jmp .while_inner;
.done_inner:
    decr: int = div decr two;
    rem: int = sub rem decr;
    jmp .while;
.done:
    ret rem;
}