## atax computes y := A^T * A * x
## for some procedurally generated matrix A and 
## some procedurally generated vector x

@main {
    # constants
    # dimensions correspond to Polybench MEDIUM_DATASET

    M: int = const 390;
    fM: int = const 390;
    N: int = const 410;
    fN: int = const 410;

    one: int = const 1;
    fzero: int = const 0;

    # initialize arrays
    A: ptr<int> = call @matrix_new M N;
    x: ptr<int> = call @vector_new N;
    call @init A x M fM N fN;

    # main computation
    # computes y := A^T * A * x

    y: ptr<int> = call @vector_new N;
    i: int = const 0;
.init_y:
    cond: bool = lt i N;
    br cond .init_y_body .init_y_done;
.init_y_body:
    call @vector_set y i fzero;
    i: int = add i one;
    jmp .init_y;
.init_y_done:
    
    tmp: ptr<int> = call @vector_new M;
    i: int = const 0;
.main_i:
    cond: bool = lt i M;
    br cond .main_i_body .main_i_done;
.main_i_body:
    call @vector_set tmp i fzero;
    j: int = const 0;
.main_j1:
    cond: bool = lt j N;
    br cond .main_j1_body .main_j1_done;
.main_j1_body:
    Aij: int = call @matrix_get A i j N;
    xj: int = call @vector_get x j;
    tmpi: int = call @vector_get tmp i;
    new_tmpi: int = mul Aij xj;
    new_tmpi: int = add tmpi new_tmpi;
    call @vector_set tmp i new_tmpi;
    j: int = add j one;
    jmp .main_j1;
.main_j1_done:
    j: int = const 0;
.main_j2:
    cond: bool = lt j N;
    br cond .main_j2_body .main_j2_done;
.main_j2_body:
    Aij: int = call @matrix_get A i j N;
    tmpi: int = call @vector_get tmp i;
    yj: int = call @vector_get y j;
    new_yj: int = mul Aij tmpi;
    new_yj: int = add yj new_yj;
    call @vector_set y j new_yj;
    j: int = add j one;
    jmp .main_j2;
.main_j2_done:
    i: int = add i one;
    jmp .main_i;
.main_i_done:
    
    call @vector_print y N;

    free A;
    free x;
    free y;
    free tmp;
}

@init(A: ptr<int>, x: ptr<int>, 
      M: int, fM: int, N: int, fN: int) {
    one: int = const 1;
    fzero: int = const 0;
    fone: int = const 1;
    ffive: int = const 5;

    i: int = const 0;
    fi: int = const 0;
.init_x:
    cond: bool = lt i N;
    br cond .init_x_body .init_x_done;
.init_x_body:
    val: int = div fi fN;
    val: int = add fone val;
    call @vector_set x i val;
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_x;
.init_x_done:

    i: int = const 0;
    fi: int = const 0;
.init_A_i:
    cond: bool = lt i M;
    br cond .init_A_i_body .init_A_i_done;
.init_A_i_body:
    j: int = const 0;
    fj: int = const 0;
.init_A_j:
    cond: bool = lt j N;
    br cond .init_A_j_body .init_A_j_done;
.init_A_j_body:
    val: int = add fi fj;
    val: int = call @fmod val fN;
    denom: int = mul ffive fM;
    val: int = div val denom;
    call @matrix_set A i j N val;
    j: int = add j one;
    fj: int = add fj fone;
    jmp .init_A_j;
.init_A_j_done:
    i: int = add i one;
    fi: int = add fi fone;
    jmp .init_A_i;
.init_A_i_done:
}

@matrix_new(Nrow: int, Ncol: int): ptr<int> {
    total: int = mul Nrow Ncol;
    ptr: ptr<int> = alloc total;
    ret ptr;
}

@matrix_loc(mtx: ptr<int>, row: int, col: int, Ncol: int): ptr<int> {
    row_offset: int = mul row Ncol;
    offset: int = add row_offset col;
    new_ptr: ptr<int> = ptradd mtx offset;
    ret new_ptr;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_get(mtx: ptr<int>, row: int, col: int, Ncol: int): int {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    val: int = load ptr;
    ret val;
}

# EXPECTS:
#   @matrix_loc defined
@matrix_set(mtx: ptr<int>, row: int, col: int, Ncol: int, val: int) {
    ptr: ptr<int> = call @matrix_loc mtx row col Ncol;
    store ptr val;
}

@vector_new(N: int): ptr<int> {
    ptr: ptr<int> = alloc N;
    ret ptr;
}

@vector_get(vec: ptr<int>, i: int): int {
    ptr: ptr<int> = ptradd vec i;
    val: int = load ptr;
    ret val;
}

@vector_set(vec: ptr<int>, i: int, val: int) {
    ptr: ptr<int> = ptradd vec i;
    store ptr val;
}

# EXPECTS:
#   @vector_get defined
@vector_print(vec: ptr<int>, N: int) {
    i: int = const 0;
    one: int = const 1;
.while:
    cond: bool = lt i N;
    br cond .body .done;
.body:
    val: int = call @vector_get vec i;
    print val;
    i: int = add i one;
    jmp .while;
.done:
}

# Search for n % m where n and m are ints by
# iteratively subtracting the largest m*2^k that
# fits inside n. 
# Takes O((log n/m)^2) time.
# NOTE: In C, this can be done with a built in 
# function, but this is the best we can do.
@fmod(n: int, m: int): int {
    zero: int = const 0;
    two: int = const 2;
    rem: int = id n;
.while:
    cond: bool = ge rem m;
    br cond .body .done;
.body:
    decr: int = id m;
.while_inner:
    diff: int = sub rem decr;
    cond: bool = ge diff zero;
    br cond .body_inner .done_inner;
.body_inner:
    decr: int = mul decr two;
    jmp .while_inner;
.done_inner:
    decr: int = div decr two;
    rem: int = sub rem decr;
    jmp .while;
.done:
    ret rem;
}